plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
	id 'org.asciidoctor.convert' version '1.5.8'
	//swagger-ui
	id 'com.epages.restdocs-api-spec' version '0.12.0'
	id 'java'
}

group = 'net.koast'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
	set('querydslDir', file("$buildDir/generated/querydsl"))
}

dependencies {
	implementation(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-hateoas',
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-web',
			'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2',
			'org.springframework.security:spring-security-core',

			'com.querydsl:querydsl-jpa:5.0.0',
			'com.querydsl:querydsl-apt:5.0.0',

			'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4',
			'org.mapstruct:mapstruct:1.5.1.Final',
			'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	)

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'

	annotationProcessor(
			'org.mapstruct:mapstruct-processor:1.5.1.Final',
			'org.projectlombok:lombok-mapstruct-binding:0.2.0',
			'org.projectlombok:lombok',
			'org.springframework.boot:spring-boot-configuration-processor'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test',
			'org.springframework.restdocs:spring-restdocs-mockmvc',
			'com.epages:restdocs-api-spec-mockmvc:0.16.2'
	)

	testCompileOnly 'org.projectlombok:lombok'

	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	ignoreFailures = true
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

// querydsl 추가 시작
querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}
configurations {
	querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}
// querydsl 추가 끝